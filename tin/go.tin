#var {go_by} {path}
#alias {go} {
	#if {"$go_by" == "path"} {gopath %0};
	#elseif {"$go_by" == "track"} {gotrack};
	#elseif {"$go_by" == "follow"} {gofollow};
	#elseif {"$go_by" == "back"} {goback};
	#var {go_by} {path};
};

#alias {gopath} {
	pathstep %0;
};

#alias {gotrack} {
	#showme {$go_track};
	#send {$go_track};
};

#var {go_follow} {none}
#alias {gofollow} {
	#showme {$go_follow};
	#send {$go_follow};
}

#var last_fled {none}
#alias {goback} {
	#if {("$last_fled" != "none") && (&reverse_direction[$last_fled])} {
		#send {$reverse_direction[$last_fled]};
		#echo {<139>$reverse_direction[$last_fled]<079>};
		#var last_fled {none};
		#if {"$flee_back" == "kill"} {#delay {.3} {kill}};
		#if {"$flee_back" == "assist"} {#delay {.3} {assist}};
	};
	#var {flee_back} {none};
}

#var {flee_back} {none};
#alias {fleereenter} {#var {flee_back} {back}; flee;}
#alias {fleekill} {#var {flee_back} {kill}; flee;}
#alias {fleeassist} {#var {flee_back} {assist}; flee;}
#alias {fleerrun} {#var {flee_back} {run}; flee;}

#var {reverse_direction} {}
#var {reverse_direction[north]} {south}
#var {reverse_direction[south]} {north}
#var {reverse_direction[west]} {east}
#var {reverse_direction[east]} {west}
#var {reverse_direction[up]} {down}
#var {reverse_direction[down]} {up}

#action {^You see some {apparent|clear|faint|plain|vague}{(, bloody)?} tracks of %* leading {north|east|south|west|up|down}, done less than %d {hour|hours} ago.$} {
	#var {go_track} {%5};
	#var {go_by} {track};
};

#action {^*%+{Orc|Troll|Man|Woman|Elf|Half-Elf|Dwarf|Hobbit|Bear|Numenorean|N\xFAmen\xF3rean}* leaves {north|south|east|west|up|down|above|below}{(?: riding .+)?}.$} {
	#var {go_follow} {%4};
	#var {go_by} {follow};
};

#action {^You flee {north|south|east|west|up|down}.$} {
	#var last_fled {%1};
	#var go_by {back};
	#delay {7} {#var {go_by} {path}};
	#echo {<139>You flee %1!<079>};
	#if {"$flee_back" == "run"} {#var {flee_back} {none}; pr};
	#elseif {"$flee_back" != "none"} {#delay {.3} {v}};
	#line gag;
};


#alias {setweapon} {
	#var {wwimpy} {%1};
	#var {waggro} {%2};
	#var {weapon} {$wwimpy};
	#echo {Weapons set to $wwimpy (wimpy) and $waggro (aggro).};
	#cr;
}
#alias {chaweapon} {
	#if { "$weapon" == "$waggro"} {
		#var {weapon} {$wwimpy};
		#send {sheathe};
		#send {wield $wwimpy};
	};
	#else {
		#var {weapon} {$waggro};
		#send {draw};
		#nop send {wield $waggro};
	};
}


#var mood {W}
#alias {mood} {
	#if { "%0" != ""} {
		#var {wimpy} {%0};
		#nop echo {Change wimpy $wimpy\.};
		#send {change wimpy $wimpy};
	};
	#else {
		#if { "$mood" == "W"} {
			#echo {Change mood aggressive: $wimpy\.};
			change mood aggressive;
			#var mood {A};
		};
		#elseif { "$mood" == "A"} {
			#echo {Change mood wimpy: $wimpy\.};
			change mood wimpy;
			#var mood {W};
		};
	};
}


#alias {trtarget} {
	if { "%0" != ""}Â {target %0};
	#send {track ${TARGET}};
}


#var {TARGET} {animal}
#var {NTARGET} {1}
#action {^{A|An} %1 has arrived from the %*.$} {#var {arrived} {%1}}
#action {^You have found {a|an} %1!$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}
#action {^You have revealed {a|an} %1!$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}
#action {^%* makes a discreet gesture, pointing at {a|an} %*'{(s)?} hiding place.$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}

#action {%* (l) is dead! R.I.P.$} {#var TARGET {animal}; rtarget};

#alias kill {
	#nop hit the target;
	#if { "%0" != ""} {target %0};
	#echo {kill ${NTARGET}\.${TARGET}};
	#send {kill ${NTARGET}\.${TARGET}};
};
#alias stab {
	#nop backstab the target;
	#if { "%0" != ""} {target %0};
	#echo {backstab ${NTARGET}\.${TARGET}};
	#send {backstab ${NTARGET}\.${TARGET}};
};
#alias xtarget {
	#nop exam the target;
	#if {"%0" != ""} {exam %0};
	#else {exam ${NTARGET}\.${TARGET}};
}


#alias target {
	#if { "%0" != ""} {
		#var {TARGET} {%0};
	};
	#else {
		#var {TARGET} {animal};
	};
	rtarget;
};
	

#alias itarget {
	#nop increase the target number;
	#math {NTARGET} {$NTARGET + 1};
	#echo {Target ${NTARGET}\.${TARGET}\.};
};

#alias rtarget {
	#nop reset the target number to 1;
	#var {NTARGET} {1};
	#showme {Target ${NTARGET}\.$TARGET\.};
};

#alias settarget {
	#var {NTARGET} {%1};
	#showme {Target ${NTARGET}\.$TARGET\.};
}


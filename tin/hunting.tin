#alias {setweapon} {
	#var {weapona} {%1};
	#var {weaponb} {%2};
	#var {weapon} {$weapona};
	#echo {Weapons set to $weapona and $weaponb\.};
	#cr;
}
#alias {chaweapon} {
	#if { "$weapon" == "$weapona"} {
		#var {weapon} {$weaponb};
		#send {draw $weaponb};
	};
	#else {
		#var {weapon} {$weapona};
		#send {draw $weapona};
	};
}


#var mood {W}
#alias {mood} {
	#if { "%0" != ""} {
		#var {wimpy} {%0};
		#nop echo {Change wimpy $wimpy\.};
		#send {change wimpy $wimpy};
	};
	#else {
		#if { "$mood" == "W"} {
			#echo {Change mood aggressive: $wimpy\.};
			change mood aggressive;
			#var mood {A};
		};
		#elseif { "$mood" == "A"} {
			#echo {Change mood wimpy: $wimpy\.};
			change mood wimpy;
			#var mood {W};
		};
	};
}


#alias {tracktarget} {
	if { "%0" != ""}Â {target %0};
	#showsend track $target[$tkey][tname];
}


#var {tkey} {1}
#var {target[1][tnum]} {1};
#var {target[1][tname]} {animal};
#action {^{A|An} %1 has arrived from the %*.$} {#var {found} {%1}}
#action {^You have found {a|an} %1!$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}
#action {^You have revealed {a|an} %1!$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}
#action {^%* makes a discreet gesture, pointing at {a|an} %*'{(s)?} hiding place.$} {
	#var {found} {%1};
	#if {"$found" == "mean brigand"} {#var {found} {brigand}};
}

#nop reset target when labelled on dies.
#action {%* \(\w\) is dead! R.I.P.$} {target};

#alias kill {
	#nop hit the target;
	#if { "%0" != ""} {target %0};
	showsend kill $target[$tkey][tnum]\.$target[$tkey][tname];
};
#alias stab {
	#nop backstab the target;
	#if { "%0" != ""} {target %0};
	showsend backstab $target[$tkey][tnum]\.$target[$tkey][tname];
};
#alias xtarget {
	#nop exam the target;
	#if {"%0" != ""} {exam %0};
	#else {
		showsend exam $target[$tkey][tnum]\.$target[$tkey][tname];
		showsend cons $target[$tkey][tnum]\.$target[$tkey][tname];
	};
}



#alias target {
	#if { "%0" == ""} {
		#var {tkey} {1};
		#var {target[$tkey][tname]} {animal};
	};
	#else {
		#var {tkey} {%1};
		#if { "%2" != "" } {
			#var {target[$tkey][tname]} {%2};
		};
	};
	rtarget;
	targetnoswitch;
};
	

#alias itarget {
	#nop increase the target number;
	#if { "%0" != ""} {
		#var {target[$tkey][tnum]} {%1};
	};
	#else {
		#math {target[$tkey][tnum]} {$target[$tkey][tnum] + 1};
	};
	#showme {Target $target[$tkey][tnum]\.$target[$tkey][tname]\.};
};

#alias rtarget {
	#nop reset the target number to 1;
	#var {target[$tkey][tnum]} {1};
	#showme {Target $target[$tkey][tnum]\.$target[$tkey][tname]\.};
	#nop showmyprompt;
};



#alias targetswitch {
	#var {target[$tkey][tnum]} {1};
	#var {target[$tkey][tname]} {%1};
	#action {^Backstab whom?$} {
		targetswitch %2 %3 %4 %5 %6 %7 %8 %9 %1;
		#echo {They aren't here. Targetting %2.};
		#line gag;
	};
	#action {^They aren't here.$} {
		targetswitch %2 %3 %4 %5 %6 %7 %8 %9 %1;
		#echo {They aren't here. Targetting %2.};
		#line gag;
	};
	#action {^Nobody here by that name.$} {
		targetswitch %2 %3 %4 %5 %6 %7 %8 %9 %1;
		#echo {They aren't here. Targetting %2.};
		#line gag;
	};
	#action {^You don't see any %* here.$} {
		targetswitch %2 %3 %4 %5 %6 %7 %8 %9 %1;
		#echo {They aren't here. Targetting %2.};
		#line gag;
	};
};

#alias targetnoswitch {
	#unaction {^Backstab whom?$};
	#unaction {^They aren't here.$};
	#unaction {^Nobody here by that name.$};
	#unaction {^You don't see any %* here.$};
};


